path /v1 {
  read() {
    false
  }
  write() {
    false
  }
}

// Logins ---------------------------------

path /v1/authentication/{uid} is String {
  read() {
    isCurrentUser(uid)
  }
  create() {
    isCurrentUser(uid)
  }
  update() {
    false
  }
  delete() {
    isCurrentUser(uid)
  }
}

// Players ---------------------------------

type SimplePlayer {
  name: String,
  id: String | Null
}

type Player extends SimplePlayer {
  mail: String | Null,
  token: String | Null,
  uid: String | Null
}

path /v1/players {
  read() {
    true
  }
  write() {
    false
  }
}

path /v1/players/{playerId} is Player {
  create() {
    isCurrentUser(this.uid)
  }
  update() {
    isCurrentUser(prior(this.uid)) && isCurrentUser(this.uid)
  }
  delete() {
    isCurrentUser(this.uid)
  }
}

path /v1/players/{playerId}/games/{gameId} is SimpleGame {
  create() {
    isParticipant(getCurrentPlayerId(), this)
  }
  update() {
    isParticipant(getCurrentPlayerId(), prior(this))
  }
  delete() {
    isParticipant(getCurrentPlayerId(), this)
  }
}

// Games -----------------------------------

type Field {
  x: Number,
  y: Number,
  color: Number,
  belongsToPlayer: String | Null
}

type Move {
  color: Number,
  player: String,
  sourceField: Field,
  targetField: Field
}

type SimpleGame {
  currentPlayer: String,
  players: Map<String, SimplePlayer>
}

type Game extends SimpleGame {
  currentColor: Number | Null,
  moves: Move[] | Null
}

path /v1/games/{gameId} is Game {
  read() {
    this == null || isParticipant(getCurrentPlayerId(), this)
  }
  create() {
    isParticipant(getCurrentPlayerId(), this)
  }
  update() {
    prior(this.currentPlayer) == getCurrentPlayerId() &&
    (this.currentPlayer == null || isParticipant(this.currentPlayer, prior(this)))
  }
  delete() {
    prior(this.currentPlayer) == getCurrentPlayerId()
  }
}

// Notifications -----------------------------

path /v1/notifications/{gameId} is Number {
  read() {
    true
  }
  write() {
    isParticipant(getCurrentPlayerId(), root.v1.games[gameId])
  }
}

// Helpers -----------------------------------

function isLoggedin() {
  auth != null && auth.uid != null
}

function getCurrentPlayerId() {
  root.v1.authentication[auth.uid].val()
}

function isParticipant(playerId, game) {
  game != null && game.players[playerId] != null
}

function isCurrentUser(uid) {
  uid == auth.uid
}

function isEmpty(value) {
  prior(value) == null
}